// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
//    ext.kotlin_version = "1.3.72"

    ext {
        kotlin_version = "1.3.72"
        minSdkVersion = 19
        targetSdkVersion = 23
        compileSdkVersion = 29
        buildToolsVersion = '29.0.2'
        javaVersion = JavaVersion.VERSION_1_8

        MATRIX_VERSION = "0.9.0"
        GROUP = 'com.tencent.matrix'
        VERSION_NAME = "${MATRIX_VERSION}"

        /**
         * To compile Sample App with matrix source code for further debug:
         *
         * 1. Run `gradle publishToMavenLocal`
         * 2. Set 'compileMatrixWithSrc = true' or with run gradle with args '-PcompileWithSrc=true'
         *
         # Or see `testBuildAndroid` in `.circleci/config.yaml`.
         */
        compileMatrixWithSrc = false

        useLocalMaven = {
            return compileMatrixWithSrc ?: properties?.compileWithSrc ?: false
        }

        if (useLocalMaven()) {
            if (!MATRIX_VERSION.endsWith("-SNAPSHOT")) {
                MATRIX_VERSION = "${MATRIX_VERSION}-SNAPSHOT"
            }
        }
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath ("com.tencent.matrix:matrix-gradle-plugin:${MATRIX_VERSION}") {
            changing = true
            exclude group: 'com.android.tools.build', module: 'gradle'
        }
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
//        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}